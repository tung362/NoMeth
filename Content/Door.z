class Door : ZilchComponent
{
    var Timer : Real = 0;
    var CanOpen : Boolean = false;
    [Property]
    var Speed : Real = 10;
        
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CollisionStarted, this.OnCollisionStarted);
        Zero.Connect(this.Owner, Events.CollisionPersisted, this.OnCollisionPersisted);
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        var deltaTime = event.Dt;
        var doorTop = this.Owner.FindChildByName("DoorFull");
        var doorTopPos = doorTop.Transform.Translation;
        
        if(this.CanOpen == true)
        {
            this.Timer += deltaTime;
            
            if(this.Timer > 0.5)
            {
                this.CanOpen = false;
                //this.Owner.SoundEmitter.PlayCue("DoorClose")
                this.Timer = 0;
            }
            
            //Up
            if(doorTopPos.Y < 7.2)
            {
                doorTop.Transform.Translation += Real3( 0, this.Speed, 0) * event.Dt;
            }
            else
            {
                doorTop.Transform.Translation = Real3( 0, 7.22, 0); //Higher
            }
        }
        else
        {
            //Down
            if(doorTopPos.Y > 0)
            {
                doorTop.Transform.Translation -= Real3( 0, this.Speed, 0) * event.Dt;
            }
            else
            {
                doorTop.Transform.Translation = Real3( 0, 0, 0 );
            }
        }
    }
    
    function OnCollisionStarted(event : CollisionEvent)
    {
        var other = event.OtherObject;
        
        if(other.Name == "Player")
        {
            this.CanOpen = true;
            //this.Owner.SoundEmitter.PlayCue("DoorOpen");
            this.Timer = 0;
        }
    }
            
    function OnCollisionPersisted(event : CollisionEvent)
    {
        var other = event.OtherObject;
        
        if(other.Name == "Player")
        {
            this.CanOpen = true;
            this.Timer = 0;
        }
    }
}
